! ###################################################################
! Copyright (c) 2014-2020 Marc De Graef/Carnegie Mellon University
! All rights reserved.
!
! Redistribution and use in source and binary forms, with or without modification, are
! permitted provided that the following conditions are met:
!
!     - Redistributions of source code must retain the above copyright notice, this list
!        of conditions and the following disclaimer.
!     - Redistributions in binary form must reproduce the above copyright notice, this
!        list of conditions and the following disclaimer in the documentation and/or
!        other materials provided with the distribution.
!     - Neither the names of Marc De Graef, Carnegie Mellon University nor the names
!        of its contributors may be used to endorse or promote products derived from
!        this software without specific prior written permission.
!
! THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
! AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
! IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
! ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
! LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
! DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
! SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
! CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
! OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
! USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
! ###################################################################

!--------------------------------------------------------------------------
! EMsoft:mod_EMsoft.f90
!--------------------------------------------------------------------------
!
! MODULE: mod_EMsoft
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief definition of the EMsoft class; this class contains all the configuration parameters
!
!> @details  This class provides access to all the configuration parameters from the config file,
!  as well as any command line or environment parameters that are relevant.  Upon starting any
!  EMsoft program, the following lines should be included:
!
!   use mod_global
!   use mod_EMsoft
!
!   character(fnlen)        :: progname = 'this is the program name'
!   character(fnlen)        :: progdesc = 'and this is the descriptor'
!   type(T_EMsoftClass)     :: EMsoft 
!
!   ! this is a call to the constructor routine
!   EMsoft = T_EMsoftClass(progname, progdesc[,makeconfig][,showconfig])    
!
! This will print the usual start up message with copyright, build date, version, etc. info.
! It will also initialize all the configuration parameters; these are then available to the 
! calling program by means of the getConfigParameter method. The setConfigParameter method can
! be used to explicitly override any of the config parameters in this class. 
!
! The optional arguments to the constructor can be used to create the EMsoftConfig.json file 
! (using makeconfig=.TRUE.), or to simply print out all the configuration parameters (using 
! showconfig=.TRUE.).
!
! Finally, the method generateFilePath can be used to complete any given file path; in the old 
! f90 code this was done in three consecutive lines that were always basically the same, and here
! we provide a method to simply return the file name completed with the full path. 
!
!> @date 12/30/19 MDG 1.0 original
!--------------------------------------------------------------------------

!---------------------------
! THIS FILE IS AUTOMATICALLY GENERATED DURING CMAKE TIME. THE ORIGINAL FILE
! LOCATED AT @CTEMSoftLib_SOURCE_DIR@/mod_EMsoft.in.f90
! YOU NEED TO MAKE CHANGES TO THAT FILE. ANY CHANGES MADE TO THIS FILE WILL
! BE OVERWRITTEN THE NEXT TIME CMAKE IS EXECUTED.
!---------------------------

module mod_EMsoft

use mod_kinds
use mod_global
use stringconstants
use,intrinsic :: ISO_C_BINDING
use, intrinsic :: iso_fortran_env, only : stdin=>input_unit, &
                                          stdout=>output_unit, &
                                          stderr=>error_unit

IMPLICIT NONE

private
public :: T_EMsoftClass 

!--------------------------------------------------------------------------
!--------------------------------------------------------------------------
!--------------------------------------------------------------------------

  type, public   ::  T_EMsoftClass
    private 
     character(fnlen)  :: EMsoftpathname
     character(fnlen)  :: EMXtalFolderpathname
     character(fnlen)  :: EMdatapathname
     character(fnlen)  :: EMtmppathname
     character(fnlen)  :: EMsoftLibraryLocation
     character(fnlen)  :: EMSlackWebHookURL
     character(fnlen)  :: EMSlackChannel
     character(fnlen)  :: UserName
     character(fnlen)  :: UserLocation
     character(fnlen)  :: UserEmail
     character(5)      :: EMNotify
     character(3)      :: Develop
     character(3)      :: Release
! other configuration parameters that may be needed in various programs but are not in the EMsoftConfig.json file
     character(fnlen)  :: h5copypath
     character(fnlen)  :: EMsoftplatform
     character(fnlen)  :: EMsofttestpath
     character(fnlen)  :: EMsoftTestingPath
     character(fnlen)  :: EMsoftversion
     character(fnlen)  :: Configpath
     character(fnlen)  :: Templatepathname
     character(fnlen)  :: Resourcepathname
     character(fnlen)  :: Homepathname
     character(fnlen)  :: OpenCLpathname
     character(fnlen)  :: Templatecodefilename
     character(fnlen)  :: WyckoffPositionsfilename
     character(fnlen)  :: Randomseedfilename
     character(1)      :: EMsoftnativedelimiter
     ! character(fnlen)  :: strvals(wraparraysize)
     character(fnlen)  :: EMsoftRevision
     character(fnlen)  :: EMsoftBuildDate
     ! character(fnlen)  :: EMsoftHDFtest
     character(fnlen)  :: wikipathname
     character(fnlen)  :: User
     character(fnlen)  :: fftwWisdomfilename
     character(fnlen)  :: wikicodefilename

    contains
    private 

! private methods
      procedure, pass(self) :: init
      procedure, pass(self) :: printEMsoftHeader
      procedure, pass(self) :: EMsoft_getEMsoftpathname
      procedure, pass(self) :: EMsoft_getXtalpathname
      procedure, pass(self) :: EMsoft_getEMdatapathname
      procedure, pass(self) :: EMsoft_getEMtmppathname
      procedure, pass(self) :: EMsoft_getSlackWebHookURL
      procedure, pass(self) :: EMsoft_getSlackChannel
      procedure, pass(self) :: EMsoft_getUsername
      procedure, pass(self) :: EMsoft_getUserlocation
      procedure, pass(self) :: EMsoft_getUseremail
      procedure, pass(self) :: EMsoft_getNotify
      procedure, pass(self) :: EMsoft_getEMdevelop
      procedure, pass(self) :: EMsoft_getRelease
      procedure, pass(self) :: EMsoft_geth5copypath
      procedure, pass(self) :: EMsoft_getEMsoftplatform
      procedure, pass(self) :: EMsoft_getEMsofttestpath
      procedure, pass(self) :: EMsoft_getEMsoftTestingPath
      procedure, pass(self) :: EMsoft_getEMsoftversion
      procedure, pass(self) :: EMsoft_getConfigpath
      procedure, pass(self) :: EMsoft_getTemplatepathname
      procedure, pass(self) :: EMsoft_getResourcepathname
      procedure, pass(self) :: EMsoft_getUserHomePath
      procedure, pass(self) :: EMsoft_getOpenCLpathname
      procedure, pass(self) :: EMsoft_getTemplatecodefilename
      procedure, pass(self) :: EMsoft_getWyckoffPositionsfilename
      procedure, pass(self) :: EMsoft_getRandomseedfilename
      procedure, pass(self) :: EMsoft_getEMsoftnativedelimiter
      procedure, pass(self) :: EMsoft_getEMsoftRevision
      procedure, pass(self) :: EMsoft_getEMsoftBuildDate
      procedure, pass(self) :: EMsoft_getEMXtalFolderpathname
      procedure, pass(self) :: EMsoft_getwikipathname
      procedure, pass(self) :: EMsoft_getUser
      procedure, pass(self) :: EMsoft_getfftwWisdomfilename
      procedure, pass(self) :: EMsoft_getwikicodefilename
      procedure, pass(self) :: EMsoft_getJSONparameter
      ! procedure, pass(self)         :: EMsoft_getEMsoftHDFtest


! public methods
! [there aren't many... just one to set each parameter, and one to get each parameter;
!  in addition there is one to init the EMsoft configuration file (used by the EMsoftinit program) ]
      procedure, pass(self), public :: getConfigParameter
      ! procedure, pass(self), public :: setConfigParameter
      ! procedure, pass(self), public :: initConfigFile 
      procedure, pass(self), public :: printConfigParameters
      procedure, pass(self), public :: generateFilePath

  end type T_EMsoftClass

! the constructor routine for this class 
  interface T_EMsoftClass
    module procedure :: EMsoft_constructor
  end interface T_EMsoftClass

contains

!--------------------------------------------------------------------------
!--------------------------------------------------------------------------
! we begin with the functions/subroutines that are public in this class
!--------------------------------------------------------------------------
!--------------------------------------------------------------------------

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_constructor
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief initialize the EMsoft class 
!
!> @date  12/30/19 MDG 1.0 new function
!--------------------------------------------------------------------------
type(T_EMsoftClass) function EMsoft_constructor(progname, progdesc, makeconfig, showconfig) result(EMsoft)

IMPLICIT NONE

character(fnlen), INTENT(IN)      :: progname
character(fnlen), INTENT(IN)      :: progdesc
logical, INTENT(IN), OPTIONAL     :: makeconfig
logical, INTENT(IN), OPTIONAL     :: showconfig

  call EMsoft % init

  if (PRESENT(makeconfig)) then 
    if (makeconfig) then 
      call EMsoft % printEMsoftHeader(progname, progdesc, makeconfig)
    else
      call EMsoft % printEMsoftHeader(progname, progdesc)
    endif
  else 
      call EMsoft % printEMsoftHeader(progname, progdesc)
  end if

  if (PRESENT(showconfig)) then 
    if (showconfig) then 
      call EMsoft % printConfigParameters
    endif
  end if

end function EMsoft_constructor

!--------------------------------------------------------------------------
!
! SUBROUTINE: init
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief initializes all the components of EMsoftClass 
!
!> @date  12/30/19 MDG 1.0 new function
!--------------------------------------------------------------------------
subroutine init(self)
  class(T_EMsoftClass),intent(inout) :: self

! fill in all the values
  self % EMsoftpathname               = trim(self % EMsoft_getEMsoftpathname())
  self % EMXtalFolderpathname         = trim(self % EMsoft_getXtalpathname())
  self % EMdatapathname               = trim(self % EMsoft_getEMdatapathname())
  self % EMtmppathname                = trim(self % EMsoft_getEMtmppathname())
  ! self % EMsoftLibraryLocation = 
  self % EMSlackWebHookURL            = trim(self % EMsoft_getSlackWebHookURL())
  self % EMSlackChannel               = trim(self % EMsoft_getSlackChannel())
  self % UserName                     = trim(self % EMsoft_getUsername())
  self % UserLocation                 = trim(self % EMsoft_getUserlocation())
  self % UserEmail                    = trim(self % EMsoft_getUseremail())
  self % EMNotify                     = trim(self % EMsoft_getNotify())
  self % Develop = 'No'
  if (self % EMsoft_getEMdevelop()) then 
    self % Develop                      = 'Yes'
  end if 
  self % Release = 'No' 
  if (self % EMsoft_getRelease()) then 
    self % Release = 'Yes'
  end if 
  self % h5copypath                   = trim(self % EMsoft_geth5copypath())
  self % EMsoftplatform               = trim(self % EMsoft_getEMsoftplatform())
  self % EMsofttestpath               = trim(self % EMsoft_getEMsofttestpath())
  self % EMsoftTestingPath            = trim(self % EMsoft_getEMsoftTestingPath())
  self % EMsoftversion                = trim(self % EMsoft_getEMsoftversion())
  self % Configpath                   = trim(self % EMsoft_getConfigpath())
  self % Templatepathname             = trim(self % EMsoft_getTemplatepathname())
  self % Resourcepathname             = trim(self % EMsoft_getResourcepathname())
  self % Homepathname                 = trim(self % EMsoft_getUserHomePath())
  self % OpenCLpathname               = trim(self % EMsoft_getOpenCLpathname())
  self % Templatecodefilename         = trim(self % EMsoft_getTemplatecodefilename())
  self % WyckoffPositionsfilename     = trim(self % EMsoft_getWyckoffPositionsfilename())
  self % Randomseedfilename           = trim(self % EMsoft_getRandomseedfilename())
  self % EMsoftnativedelimiter        = trim(self % EMsoft_getEMsoftnativedelimiter())
  self % EMsoftRevision               = trim(self % EMsoft_getEMsoftRevision())
  self % EMsoftBuildDate              = trim(self % EMsoft_getEMsoftBuildDate())
  self % EMXtalFolderpathname         = trim(self % EMsoft_getEMXtalFolderpathname())
  self % wikipathname                 = trim(self % EMsoft_getwikipathname())
  self % User                         = trim(self % EMsoft_getUser())
  self % fftwWisdomfilename           = trim(self % EMsoft_getfftwWisdomfilename())
  self % wikicodefilename             = trim(self % EMsoft_getwikicodefilename())

end subroutine init


!--------------------------------------------------------------------------
!
! SUBROUTINE: printConfigParameters
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief prints all the components of EMsoftClass 
!
!> @date  12/31/19 MDG 1.0 new function
!--------------------------------------------------------------------------
subroutine printConfigParameters(self)

  use mod_io 

  IMPLICIT NONE 

  class(T_EMsoftClass),intent(inout) :: self
  type(T_IOClass)                    :: Message 

  character(fnlen)                   :: m

  call Message % printMessage( ' EMsoft Configuration Parameters ' )
  call Message % printMessage( ' ------------------------------- ' )
  call Message % printMessage( 'EMsoftpathname           = '//trim( self % EMsoftpathname ) )
  call Message % printMessage( 'EMXtalFolderpathname     = '//trim( self % EMXtalFolderpathname ) )
  call Message % printMessage( 'EMdatapathname           = '//trim( self % EMdatapathname ) )
  call Message % printMessage( 'EMtmppathname            = '//trim( self % EMtmppathname ) )
  call Message % printMessage( 'EMsoftLibraryLocation    = '//trim( self % EMsoftLibraryLocation ) )
  call Message % printMessage( 'EMSlackWebHookURL        = '//trim( self % EMSlackWebHookURL ) )
  call Message % printMessage( 'EMSlackChannel           = '//trim( self % EMSlackChannel ) )
  call Message % printMessage( 'UserName                 = '//trim( self % UserName ) )
  call Message % printMessage( 'UserLocation             = '//trim( self % UserLocation ) )
  call Message % printMessage( 'UserEmail                = '//trim( self % UserEmail ) )
  call Message % printMessage( 'EMNotify                 = '//trim( self % EMNotify ) )
  call Message % printMessage( 'Develop                  = '//trim( self % Develop ) )
  call Message % printMessage( 'Release                  = '//trim( self % Release ) )
  call Message % printMessage( 'h5copypath               = '//trim( self % h5copypath ) )
  call Message % printMessage( 'EMsoftplatform           = '//trim( self % EMsoftplatform ) )
  call Message % printMessage( 'EMsofttestpath           = '//trim( self % EMsofttestpath ) )
  call Message % printMessage( 'EMsoftTestingPath        = '//trim( self % EMsoftTestingPath ) )
  call Message % printMessage( 'EMsoftversion            = '//trim( self % EMsoftversion ) )
  call Message % printMessage( 'Configpath               = '//trim( self % Configpath ) )
  call Message % printMessage( 'Templatepathname         = '//trim( self % Templatepathname ) )
  call Message % printMessage( 'Resourcepathname         = '//trim( self % Resourcepathname ) )
  call Message % printMessage( 'Homepathname             = '//trim( self % Homepathname ) )
  call Message % printMessage( 'OpenCLpathname           = '//trim( self % OpenCLpathname ) )
  call Message % printMessage( 'Templatecodefilename     = '//trim( self % Templatecodefilename ) )
  call Message % printMessage( 'WyckoffPositionsfilename = '//trim( self % WyckoffPositionsfilename ) )
  call Message % printMessage( 'Randomseedfilename       = '//trim( self % Randomseedfilename ) )
  call Message % printMessage( 'EMsoftnativedelimiter    = '//trim( self % EMsoftnativedelimiter ) )
  call Message % printMessage( 'EMsoftRevision           = '//trim( self % EMsoftRevision ) )
  call Message % printMessage( 'EMsoftBuildDate          = '//trim( self % EMsoftBuildDate ) )
  call Message % printMessage( 'wikipathname             = '//trim( self % wikipathname ) )
  call Message % printMessage( 'User                     = '//trim( self % User ) )
  call Message % printMessage( 'fftwWisdomfilename       = '//trim( self % fftwWisdomfilename ) )
  call Message % printMessage( 'wikicodefilename         = '//trim( self % wikicodefilename ) )
  call Message % printMessage( ' '  )

end subroutine printConfigParameters

!--------------------------------------------------------------------------
!
! FUNCTION: getConfigParameter
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief get a particular component of EMsoftClass
!
!> @date  12/31/19 MDG 1.0 new function
!--------------------------------------------------------------------------
function getConfigParameter(self, inp) result(cp)
  class(T_EMsoftClass),intent(inout) :: self
  character(*),INTENT(IN)            :: inp
  character(fnlen)                   :: cp

  select case(trim(inp))
    case('EMsoftpathname')
      cp = trim( self % EMsoftpathname )
    case('EMXtalFolderpathname')
      cp = trim( self % EMXtalFolderpathname )
    case('EMdatapathname')
      cp = trim( self % EMdatapathname )
    case('EMtmppathname')
      cp = trim( self % EMtmppathname )
    case('EMsoftLibraryLocation')
      cp = trim( self % EMsoftLibraryLocation )
    case('EMSlackWebHookURL')
      cp = trim( self % EMSlackWebHookURL )
    case('EMSlackChannel')
      cp = trim( self % EMSlackChannel )
    case('UserName')
      cp = trim( self % UserName )
    case('UserLocation')
      cp = trim( self % UserLocation )
    case('UserEmail')
      cp = trim( self % UserEmail )
    case('EMNotify')
      cp = trim( self % EMNotify )
    case('Develop')
      cp = trim( self % Develop )
    case('Release')
      cp = trim( self % Release )
    case('h5copypath')
      cp = trim( self % h5copypath )
    case('EMsoftplatform')
      cp = trim( self % EMsoftplatform )
    case('EMsofttestpath')
      cp = trim( self % EMsofttestpath )
    case('EMsoftTestingPath')
      cp = trim( self % EMsoftTestingPath )
    case('EMsoftversion')
      cp = trim( self % EMsoftversion )
    case('Configpath')
      cp = trim( self % Configpath )
    case('Templatepathname')
      cp = trim( self % Templatepathname )
    case('Resourcepathname')
      cp = trim( self % Resourcepathname )
    case('Homepathname')
      cp = trim( self % Homepathname )
    case('OpenCLpathname')
      cp = trim( self % OpenCLpathname )
    case('Templatecodefilename')
      cp = trim( self % Templatecodefilename )
    case('WyckoffPositionsfilename')
      cp = trim( self % WyckoffPositionsfilename )
    case('Randomseedfilename')
      cp = trim( self % Randomseedfilename )
    case('EMsoftnativedelimiter')
      cp = trim( self % EMsoftnativedelimiter )
    case('EMsoftRevision')
      cp = trim( self % EMsoftRevision )
    case('EMsoftBuildDate')
      cp = trim( self % EMsoftBuildDate )
    case('wikipathname')
      cp = trim( self % wikipathname )
    case('User')
      cp = trim( self % User )
    case('fftwWisdomfilename')
      cp = trim( self % fftwWisdomfilename )
    case('wikicodefilename')
      cp = trim( self % wikicodefilename )
    case default
      cp = 'unknown configuration parameter'
  end select 

end function getConfigParameter

!--------------------------------------------------------------------------
!
! FUNCTION: generateFilePath
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief complete a file path 
!
!> @date  12/31/19 MDG 1.0 new function
!--------------------------------------------------------------------------
function generateFilePath(self, cp, fn) result(fp)

  use mod_io 

  IMPLICIT NONE 

  class(T_EMsoftClass),intent(inout) :: self
  character(*),INTENT(IN)            :: cp    ! configuration parameter string 
  character(*),INTENT(IN),OPTIONAL   :: fn    ! file name with incomplete path 
  character(fnlen)                   :: fp    ! completed file name 

  character(fnlen)                   :: path 

  type(T_IOClass)                    :: Message

  path = trim(self % getConfigParameter(cp))

  if (trim(path).eq.'unknown configuration parameter') then    ! report error and exit 
    Message = T_IOClass()
    call Message % printError('generateFilePath',' unknown configuration parameter')
  else 
    if (present(fn)) then 
      fp = trim(path)//trim(fn)           ! prepend the path 
    else 
      fp = trim(path)                     ! this is already a complete file name ... 
    end if 
    fp = EMsoft_toNativePath(self, fp)  ! and use the correct delimiter for this platform 
  end if

end function generateFilePath

!--------------------------------------------------------------------------
!--------------------------------------------------------------------------
! from here on, we have the older functions (all private) that are called 
! by the new class functions 
!--------------------------------------------------------------------------
!--------------------------------------------------------------------------


!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMsoftpathname
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the EMsoftpathname variable from the EMsoftconfig.json file
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!> @date  02/27/19 MDG 2.0 add functionality for environment variables
!--------------------------------------------------------------------------
function EMsoft_getEMsoftpathname(self) result(EMsoftpathname)

use, intrinsic :: iso_fortran_env , only: error_unit, wp => real64
use mod_io 

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: EMsoftpathname, ep, envParam, envReturn, m
integer                                 :: l, status
type(T_IOClass)                         :: Message 


ep = SC_EMsoftpathname
EMsoftpathname = EMsoft_getJSONparameter(self, ep)

if (trim(EMsoftpathname).eq.'tryEnvironmentVariable') then 
  envParam = 'EMSOFTPATHNAME'
  call getenv(trim(envParam),envReturn)
  if (trim(envReturn).ne.'') then 
    EMsoftpathname = trim(envReturn)
    l = len(trim(EMsoftpathname))
    if (EMsoftpathname(l:l).ne.'/') EMsoftpathname = trim(EMsoftpathname)//'/'
  else
    Message = T_IOClass()
    status = 999001
    call Message % printError('EMsoftpathname was not defined in the json file', &
                   status, (/ 'EMSOFTPATHNAME environment variable was NOT defined as a backup.'/))
  end if
end if 

end function EMsoft_getEMsoftpathname

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getXtalpathname
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the xtalpathname
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!> @date  08/15/17 MDG 1.1 removed need for xtal folder to be named XtalFolder
!--------------------------------------------------------------------------
function EMsoft_getXtalpathname(self) result(xtalpathname)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: xtalpathname, p

xtalpathname = trim(EMsoft_getEMXtalFolderpathname(self))

end function EMsoft_getXtalpathname

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMdatapathname
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the EMdatapathname variable from the EMsoftconfig.json file
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!> @date  02/27/19 MDG 2.0 add functionality for environment variables
!--------------------------------------------------------------------------
function EMsoft_getEMdatapathname(self) result(EMdatapathname)

use, intrinsic :: iso_fortran_env , only: error_unit, wp => real64
use mod_io

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

type(T_IOClass)                         :: Message
character(fnlen)                        :: EMdatapathname, ep, envParam, envReturn
integer                                 :: l

ep = SC_EMdatapathname
EMdatapathname = EMsoft_getJSONparameter(self, ep)

if (trim(EMdatapathname).eq.'tryEnvironmentVariable') then 
  envParam = 'EMDATAPATHNAME'
  call getenv(trim(envParam),envReturn)
  if (trim(envReturn).ne.'') then 
    EMdatapathname = trim(envReturn)
    l = len(trim(EMdatapathname))
    if (EMdatapathname(l:l).ne.'/') EMdatapathname = trim(EMdatapathname)//'/'
  else
    if (displayEMsoftWarningMessages.eq.0) then 
      Message = T_IOClass()
      call Message % printWarning('EMdatapathname was not defined in the json file', &
                     (/ 'EMDATAPATHNAME environment variable was NOT defined as a backup.', &
                        '----> using absolute path convention                            '/) )
      displayEMsoftWarningMessages = displayEMsoftWarningMessages+1
    end if
    EMdatapathname = ''
  end if
end if 

end function EMsoft_getEMdatapathname


!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMtmppathname
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the EMtmppathname variable from the EMsoftconfig.json file
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!> @date  03/02/19 MDG 2.0 add functionality for environment variables
!--------------------------------------------------------------------------
function EMsoft_getEMtmppathname(self) result(EMtmppathname)

use, intrinsic :: iso_fortran_env , only: error_unit, wp => real64
use mod_io

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

type(T_IOClass)                         :: Message
character(fnlen)                        :: EMtmppathname, ep, envParam, envReturn
integer                                 :: l

ep = SC_EMtmppathname
EMtmppathname = EMsoft_getJSONparameter(self, ep)

if (trim(EMtmppathname).eq.'tryEnvironmentVariable') then 
  envParam = 'EMTMPPATHNAME'
  call getenv(trim(envParam),envReturn)
  if (trim(envReturn).ne.'') then 
    EMtmppathname = trim(envReturn)
    l = len(trim(EMtmppathname))
    if (EMtmppathname(l:l).ne.'/') EMtmppathname = trim(EMtmppathname)//'/'
  else
    if (displayEMsoftWarningMessages.eq.0) then 
      Message = T_IOClass()
      call Message % printWarning('EMtmppathname was not defined in the json file', &
                     (/ 'EMTMPPATHNAME environment variable was NOT defined as a backup.', &
                        '----> using absolute path convention                           '/) )
      displayEMsoftWarningMessages = displayEMsoftWarningMessages+1
    end if
    EMtmppathname = ''
  end if
end if 

end function EMsoft_getEMtmppathname

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getSlackWebHookURL
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the URL for the Slack Webhook to send message to the user
!
!> @param no input parameters
!
!> @date  08/18/17 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getSlackWebHookURL(self) result(SlackWebHookURL)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen) :: SlackWebHookURL, ep

ep = SC_EMSlackWebHookURL
SlackWebHookURL = EMsoft_getJSONparameter(self, ep, nobackslash=.TRUE.)

end function EMsoft_getSlackWebHookURL

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getSlackChannel
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the URL for the Slack Webhook to send message to the user
!
!> @param no input parameters
!
!> @date  08/18/17 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getSlackChannel(self) result(SlackChannel)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen) :: SlackChannel, ep

ep = SC_EMSlackChannel
SlackChannel = EMsoft_getJSONparameter(self, ep, nobackslash=.TRUE.)

end function EMsoft_getSlackChannel

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getUsername
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the Username variable from the EMsoftconfig.json file
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!> @date  03/02/19 MDG 2.0 add functionality for environment variables
!--------------------------------------------------------------------------
function EMsoft_getUsername(self) result(username)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: username, ep, envParam, envReturn, loginname

ep = SC_UserName
username = EMsoft_getJSONparameter(self, ep, nobackslash=.TRUE.)

if (trim(username).eq.'tryEnvironmentVariable') then 
  envParam = 'USERNAME'
  call getenv(trim(envParam),envReturn)
  if (trim(envReturn).ne.'') then 
    username = trim(envReturn)
  else
    call getlog(loginname)
    username = trim(loginname)
  end if
end if 

end function EMsoft_getUsername

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getUserlocation
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the userlocation variable from the EMsoftconfig.json file
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!> @date  03/02/19 MDG 2.0 add functionality for environment variables
!--------------------------------------------------------------------------
function EMsoft_getUserlocation(self) result(userlocation)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: userlocation, ep, envParam, envReturn, hostname

ep = SC_UserLocation
userlocation = EMsoft_getJSONparameter(self, ep, nobackslash=.TRUE.)

if (trim(userlocation).eq.'tryEnvironmentVariable') then 
  envParam = 'USERlocation'
  call getenv(trim(envParam),envReturn)
  if (trim(envReturn).ne.'') then 
    userlocation = trim(envReturn)
  else
    call hostnm(hostname)
    userlocation = trim(hostname)
  end if
end if 

end function EMsoft_getUserlocation

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getUseremail
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the useremail variable from the EMsoftconfig.json file
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!> @date  03/02/19 MDG 2.0 add functionality for environment variables
!--------------------------------------------------------------------------
function EMsoft_getUseremail(self) result(useremail)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: useremail, ep, envParam, envReturn, hostname

ep = SC_UserEmail
useremail = EMsoft_getJSONparameter(self, ep, nobackslash=.TRUE.)

if (trim(useremail).eq.'tryEnvironmentVariable') then 
  useremail = 'undefined'
end if

end function EMsoft_getUseremail

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getNotify
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the EMNotify parameter
!
!> @param no input parameters
!
!> @date  08/18/17 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getNotify(self) result(Notify)
!DEC$ ATTRIBUTES DLLEXPORT :: EMsoft_getNotify

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen) :: Notify, ep

ep = SC_EMNotify
Notify = EMsoft_getJSONparameter(self, ep, nobackslash=.TRUE.)

end function EMsoft_getNotify

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMdevelop
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief check whether or not the Develop keyword is present in the config file
!
!> @param no input parameters
!
!> @date  08/18/16 MDG 1.0 new function
!> @date  09/10/19 MDG 1.1 add environment variable option for EMdevelop
!--------------------------------------------------------------------------
function EMsoft_getEMdevelop(self) result(EMdevelop)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: EMstring, ep, envParam, envReturn
logical                                 :: EMdevelop

! default: not in developer mode
EMdevelop = .FALSE.

ep = SC_Develop
EMstring = EMsoft_getJSONparameter(self, ep, nobackslash=.TRUE.)

if (trim(EMstring).eq.'Yes') then 
  EMdevelop = .TRUE.
else if (trim(EMstring).eq.'tryEnvironmentVariable') then
       envParam = 'EMdevelop'
       call getenv(trim(envParam),envReturn)
       if (trim(envReturn).eq.'Yes') EMdevelop = .TRUE.
     end if

end function EMsoft_getEMdevelop

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getRelease
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief check whether or not the Release keyword is present in the config file
!
!> @param no input parameters
!
!> @date  10/28/17 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getRelease(self) result(Release)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: EMstring, ep
logical                                 :: Release

ep = SC_Release
EMstring = EMsoft_getJSONparameter(self, ep, nobackslash=.TRUE.)

Release = .FALSE.
if (trim(EMstring).eq.'Yes') Release = .TRUE.

end function EMsoft_getRelease

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_geth5copypath
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the location of the h5copy program
!
!> @param no input parameters
!
!> @date  08/18/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_geth5copypath(self) result(h5copypath)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen) :: h5copypath

if (trim(EMsoft_getEMsoftplatform(self)).eq.SC_Windows) then
  h5copypath = "@HDF5_INSTALL@"//SC_h5copy//".exe"
else
  h5copypath = "@HDF5_INSTALL@"//SC_h5copy
end if

end function EMsoft_geth5copypath

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMsoftplatform
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the EMsoftplatform
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getEMsoftplatform(self) result(platform)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen) :: platform

platform = "@CMAKE_SYSTEM_NAME@"

end function EMsoft_getEMsoftplatform

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMsofttestpath
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the EMsofttestpath variable
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getEMsofttestpath(self) result(testpath)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen) :: testpath, binarypath
character(1)     :: EMsoftnativedelimiter

EMsoftnativedelimiter = EMsoft_getEMsoftnativedelimiter(self)

binarypath = "@EMsoftOO_BINARY_DIR@"

testpath = trim(EMsoft_toNativePath(self, binarypath))&
           //EMsoftnativedelimiter//SC_Testing&
           //EMsoftnativedelimiter//SC_Temporary

end function EMsoft_getEMsofttestpath

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMsoftTestingPath
!
!> @brief Returns the path to the EMsoft binary directory
!
!> @param no input parameters
!--------------------------------------------------------------------------
function EMsoft_getEMsoftTestingPath(self) result(buildpath)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen) :: buildpath

buildpath = "@EMsoftOO_TESTING_DIR@"

end function EMsoft_getEMsoftTestingPath

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMsoftversion
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the Version Information
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getEMsoftversion(self) result(version)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(20) :: version

version = "@EMsoftOO_VER_MAJOR@_@EMsoftOO_VER_MINOR@_@EMsoftOO_VER_PATCH@_@EMsoftOO_VERSION_TWEAK@"

end function EMsoft_getEMsoftversion

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getConfigpath
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the path for the configuration file
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getConfigpath(self) result(configpath)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)  :: configpath

character(fnlen)  :: dirstring
character(1)      :: EMsoftnativedelimiter

! determine the pathname delimiter character
EMsoftnativedelimiter = EMsoft_getEMsoftnativedelimiter(self)

! get the user's home directory to prepend to the config file name
dirstring = EMsoft_getUserHomePath(self)

configpath = trim(dirstring)//EMsoftnativedelimiter//SC_config//EMsoftnativedelimiter//SC_EMsoft//EMsoftnativedelimiter

end function EMsoft_getConfigpath

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getTemplatepathname
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the templatepathname
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!> @date  05/11/17 MDG 1.1 added support for JSON template files
!--------------------------------------------------------------------------
function EMsoft_getTemplatepathname(self, json) result(templatepathname)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

logical,INTENT(IN),OPTIONAL             :: json

character(fnlen)                        :: templatepathname

if (present(json)) then
  if (json.eqv..TRUE.) then
    templatepathname = trim(EMsoft_getEMsoftpathname(self))//SC_JSONTemplates//'/'
  else
    templatepathname = trim(EMsoft_getEMsoftpathname(self))//SC_NamelistTemplates//'/'
  end if
else
  templatepathname = trim(EMsoft_getEMsoftpathname(self))//SC_NamelistTemplates//'/'
end if

end function EMsoft_getTemplatepathname

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getResourcepathname
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the resourcepathname
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getResourcepathname(self) result(resourcepathname)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: resourcepathname

resourcepathname = trim(EMsoft_getEMsoftpathname(self))//SC_resources//'/'

end function EMsoft_getResourcepathname

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getUserHomePath
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the resourcepathname
!
!> @param no input parameters
!
!> @date  10/25/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getUserHomePath(self) result(userHomePathName)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)  :: userHomePathName
character(9)      :: Home
character(2)      :: HomeDrive

HomeDrive = ''
if (trim(EMsoft_getEMsoftplatform(self)).eq.SC_Windows) then
  Home = 'HOMEPATH'
  call getenv("HOMEDRIVE",HomeDrive)
else
  Home = 'HOME'
end if

call getenv(trim(Home),userHomePathName)
userHomePathName = trim(HomeDrive)//trim(userHomePathName)

end function EMsoft_getUserHomePath

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getOpenCLpathname
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the openclpathname
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getOpenCLpathname(self) result(openclpathname)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: openclpathname

openclpathname = trim(EMsoft_getEMsoftpathname(self))//SC_opencl//'/'

end function EMsoft_getOpenCLpathname

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getTemplatecodefilename
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the templatecodefilename
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getTemplatecodefilename(self) result(templatecodefilename)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: templatecodefilename

templatecodefilename = trim(EMsoft_getResourcepathname(self))//SC_templatecodestxt

end function EMsoft_getTemplatecodefilename

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getWyckoffPositionsfilename
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the Wyckoff Positions filename
!
!> @param no input parameters
!
!> @date  09/05/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getWyckoffPositionsfilename(self) result(WPfilename)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: WPfilename

WPfilename = trim(EMsoft_getResourcepathname(self))//SC_WyckoffPositionstxt

end function EMsoft_getWyckoffPositionsfilename

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getRandomseedfilename
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the randomseedfilename
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getRandomseedfilename(self) result(randomseedfilename)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: randomseedfilename

randomseedfilename = trim(EMsoft_getResourcepathname(self))//SC_RandomSeedsdata

end function EMsoft_getRandomseedfilename

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMsoftnativedelimiter
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the native delimiter for file paths
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getEMsoftnativedelimiter(self) result(EMsoftnativedelimiter)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(1)  :: EMsoftnativedelimiter

if (trim(EMsoft_getEMsoftplatform(self)).eq.SC_Windows) then
  EMsoftnativedelimiter = '\'  ! ' <- this single prime inside a comment prevents syntax issues with some source code editors 
else
  EMsoftnativedelimiter = '/'
end if

end function EMsoft_getEMsoftnativedelimiter

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMsoftRevision
!
!> @author 
!
!> @brief returns the Git Hash of the current commit.
!
!> @param no input parameters
!
!> @date  
!--------------------------------------------------------------------------
function EMsoft_getEMsoftRevision(self) result(revision)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(7) :: revision

revision = "@EMsoftOO_SHORT_GIT_HASH@"

end function EMsoft_getEMsoftRevision

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMsoftBuildDate
!
!> @author 
!
!> @brief returns the build time stamp
!
!> @param no input parameters
!
!> @date  
!--------------------------------------------------------------------------
function EMsoft_getEMsoftBuildDate(self) result(buildDate)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(24) :: buildDate

buildDate = "@EMsoftOO_BUILD_TIMESTAMP@"

end function EMsoft_getEMsoftBuildDate


!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMXtalFolderpathname
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the EMXtalFolderpathname variable from the EMsoftconfig.json file
!
!> @param no input parameters
!
!> @date  08/15/17 MDG 1.0 new function
!> @date  03/02/19 MDG 2.0 add functionality for environment variables
!--------------------------------------------------------------------------
function EMsoft_getEMXtalFolderpathname(self) result(EMXtalFolderpathname)

use, intrinsic :: iso_fortran_env , only: error_unit, wp => real64
use mod_io

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

type(T_IOClass)                         :: Message
character(fnlen)                        :: EMXtalFolderpathname, ep, envParam, envReturn
integer                                 :: l

ep = SC_EMXtalFolderpathname
EMXtalFolderpathname = EMsoft_getJSONparameter(self, ep)

if (trim(EMXtalFolderpathname).eq.'tryEnvironmentVariable') then 
  envParam = 'EMXTALFOLDERPATHNAME'
  call getenv(trim(envParam),envReturn)
  if (trim(envReturn).ne.'') then 
    EMXtalFolderpathname = trim(envReturn)
    l = len(trim(EMXtalFolderpathname))
    if (EMXtalFolderpathname(l:l).ne.'/') EMXtalFolderpathname = trim(EMXtalFolderpathname)//'/'
  else
    if (displayEMsoftWarningMessages.eq.0) then 
      Message = T_IOClass()
      call Message % printWarning('EMXtalFolderpathname was not defined in the json file', &
                     (/ 'EMXTALFOLDERPATHNAME environment variable was NOT defined as a backup.', &
                        '----> using absolute path convention                                  '/) )
      displayEMsoftWarningMessages = displayEMsoftWarningMessages+1
    end if
    EMXtalFolderpathname = ''
  end if
end if 

end function EMsoft_getEMXtalFolderpathname

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getwikipathname
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the wikipathname
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!> @date  05/11/17 MDG 1.1 added support for JSON template files
!--------------------------------------------------------------------------
function EMsoft_getwikipathname(self) result(wikipathname)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: wikipathname

wikipathname = trim(EMsoft_getEMsoftpathname(self))//SC_wiki//'/'

end function EMsoft_getwikipathname

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getUser
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the system user name 
!
!> @param no input parameters
!
!> @date  10/25/16 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getUser(self) result(userName)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)  :: userName
character(9)      :: User

User = 'USER'

call getenv(trim(User),userName)
userName = trim(userName)

end function EMsoft_getUser

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getfftwWisdomfilename
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the filename of the fftw wisdom file
!
!> @param no input parameters
!
!> @date  01/29/19 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getfftwWisdomfilename(self) result(fftwWisdomfilename)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: fftwWisdomfilename

fftwWisdomfilename = trim(EMsoft_getResourcepathname(self))//SC_fftwwisdomtxt

end function EMsoft_getfftwWisdomfilename

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getwikicodefilename
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the wikicodefilename
!
!> @param no input parameters
!
!> @date  09/08/19 MDG 1.0 new function
!--------------------------------------------------------------------------
function EMsoft_getwikicodefilename(self) result(wikicodefilename)

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

character(fnlen)                        :: wikicodefilename

wikicodefilename = trim(EMsoft_getResourcepathname(self))//SC_wikicodestxt

end function EMsoft_getwikicodefilename

!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getJSONparameter
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the ep variable from the EMsoftconfig.json file
!
!> @param ep JSON variable name string
!
!> @date  07/02/16 MDG 1.0 new function
!> @date  08/19/16 MDG 1.1 modification for Develop check
!> @date  12/03/16 MDG 1.2 added check for final delimiter
!> @date  08/15/17 MDG 1.3 changed default behavior for empty JSON parameter strings
!> @date  02/27/19 MDG 2.0 add code to allow for operation without config json file
!--------------------------------------------------------------------------
function EMsoft_getJSONparameter(self, ep, nobackslash) result(param)

use json_module

use, intrinsic :: iso_fortran_env , only: error_unit, wp => real64
use mod_io 

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

type(T_IOClass)                         :: Message
character(fnlen),INTENT(IN)             :: ep
character(fnlen)                        :: param
logical,INTENT(IN),optional             :: nobackslash

type(json_file)                         :: json
integer(kind=irg)                       :: error_cnt, slen
character(kind=jsonCK,len=:),allocatable:: cval
character(fnlen)                        :: jsonfilename, jsonname, mm(2)
logical                                 :: found, jexists, bs
character(1)                            :: EMsoftnativedelimiter

Message = T_IOClass()

bs = .TRUE.
if (present(nobackslash)) then
  if (nobackslash.eqv..TRUE.) bs = .FALSE.
end if 

! determine the pathname delimiter character
EMsoftnativedelimiter = EMsoft_getEMsoftnativedelimiter(self)

jsonfilename = SC_jsonfilename
jsonname =  trim(EMsoft_getConfigpath(self))//EMsoftnativedelimiter//trim(jsonfilename)

! test whether or not this file actually exists
inquire(file=trim(jsonname),exist=jexists)

if (jexists) then
! initialize the json state variables
  error_cnt = 0
  call json_initialize()
  if (json_failed()) then
    call json_print_error_message(error_unit)
    stop
  end if

! and load the file
  call json%load_file(filename = trim(jsonname))
  if (json_failed().eqv..TRUE.) then    !if there was an error reading the file
    call json_print_error_message(error_unit)
    stop
  end if

  call json%get(trim(ep), cval, found)
  if (.not. found) then
   if (trim(ep).eq.SC_Develop) then
     param = trim(ep)
   else
     mm(1) = 'field '//trim(ep)//' not found in json file: '//trim(jsonname)
     mm(2) = 'continuing with empty parameter value for '//trim(ep) 
     call Message % printWarning('USER='//trim(EMsoft_getUser(self)), mm)
     param = ''
   end if
  else
    param = trim(cval)
  end if

! and make sure there is a terminating EMsoftnativedeliter character, except when the 
! field has zero length, in which case some other default behavior will be assumed; this 
! last case was added on 08/15/17 to offer an alternative file location mechanism to the user, namely
! either a full path declaration or the current working folder, to complement the default
! behavior which is to prepend the contents of the datapathname configuration variable.
  slen = len(trim(cval))
  if ((slen.ne.0).and.(bs.eqv..TRUE.)) then 
    if (cval(slen:slen).ne.EMsoftnativedelimiter) then
      param = trim(cval)//EMsoftnativedelimiter
    end if
  end if
else
  if (displayConfigFileMissingMessage.eq.0) then 
    mm(1) = 'file '//trim(jsonname)//' not found '
    mm(2) = '  -----> Trying environment variables next ... '
    call Message % printWarning('  WARNING: USER='//trim(EMsoft_getUser(self)), mm)
    displayConfigFileMissingMessage = 1
  end if
  param = 'tryEnvironmentVariable'
end if


end function EMsoft_getJSONparameter




!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_getEMsoftHDFtest
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief returns the EMsoftHDFtest environment variable 
!
!> @param no input parameters
!
!> @date  07/02/16 MDG 1.0 new function
!> @date  02/27/19 MDG 2.0 add functionality for environment variables
!--------------------------------------------------------------------------
function EMsoft_getEMsoftHDFtest() result(doHDFtest)
!DEC$ ATTRIBUTES DLLEXPORT :: EMsoft_getEMsoftHDFtest

use, intrinsic :: iso_fortran_env , only: error_unit, wp => real64

IMPLICIT NONE

logical                                 :: doHDFtest
character(fnlen)                        :: envParam, envReturn

envParam = 'EMsoftHDFtest'
call getenv(trim(envParam),envReturn)
doHDFtest = .FALSE.
if (trim(envReturn).ne.'') then 
  doHDFtest = .TRUE.
end if 

end function EMsoft_getEMsoftHDFtest






!--------------------------------------------------------------------------
!
! SUBROUTINE: printEMsoftHeader
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief prints a copyright statement and the program name
!
!> @details prints a copyright statement as well as where the user can find the license information
!> This is then followed by the program name, a one-line description, and a time stamp.
!
!> @param progname program name string
!> @param progdesc program descriptor string
!> @param makeconfig optional passed on to EMsoft_path_init
!
!> @date  12/08/01 MDG 1.0 original
!> @date  03/19/13 MDG 2.0 minor modifications
!> @date  05/16/13 MDG 2.1 added timestamp and stdout
!> @date  01/10/14 MDG 3.0 new version
!> @date  06/05/14 MDG 4.0 progname and progdesc are now subroutine arguments; made stdout optional
!> @date  02/25/16 MDG 4.1 added config optional parameter
!> @date  10/28/17 MDG 5.0 added config structure parameter + initialization of its fields
!--------------------------------------------------------------------------

subroutine printEMsoftHeader(self, progname, progdesc, makeconfig)

use mod_io 
use mod_timing 

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)    :: self
character(fnlen),INTENT(IN)           :: progname
character(fnlen),INTENT(IN)           :: progdesc
logical,INTENT(IN),OPTIONAL           :: makeconfig

type(T_IOClass)                       :: Message
type(T_TimingClass)                   :: Timing

 Message = T_IOClass() 

 call Message % printMessage('Copyright (C) 2001-2020 Marc De Graef Research Group/CMU',frm="(/A)")
 call Message % printMessage('EMsoft comes with ABSOLUTELY NO WARRANTY.')
 call Message % printMessage('This is free software, and you are welcome to redistribute it')
 call Message % printMessage('under certain conditions; see License.txt file for details.',frm="(A/)")

 call Message % printMessage('Program name         : '//trim(progname))
 call Message % printMessage('Purpose              : '//trim(progdesc))
 call Message % printMessage('Platform             : '//"@CMAKE_SYSTEM_NAME@")
 call Message % printMessage('Source code version  : '//trim(self % EMsoft_getEMsoftversion()))
 call Message % printMessage('Source code Revision : '//trim(self % EMsoft_getEMsoftRevision()))
 call Message % printMessage('Build Date/Time      : '//trim(self % EMsoft_getEMsoftBuildDate()),frm="(A/)")

 call Message % printMessage( 'See https://github.com/EMsoft-org/EMsoft/wiki for selected help pages.',frm="(A/)")

 Timing = T_Timingclass(showDateTime=.TRUE.)

 if (present(makeconfig)) then ! we need to (re-)create the EMsoftConfig.json file...
    if (makeconfig.eqv..TRUE.) then
      call EMsoft_path_init(self, makeconfig)
    end if
 end if

end subroutine printEMsoftHeader


!--------------------------------------------------------------------------
! EMsoft:local:EMsoft_toNativePath.f90
!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_toNativePath
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief convert a path string from the native format to the other one
!
!> @param inpath input path string
!
!> @date  02/16/16 MDG 1.0 new routine
!--------------------------------------------------------------------------
function EMsoft_toNativePath(self, inpath) result(outpath)
!DEC$ ATTRIBUTES DLLEXPORT :: EMsoft_toNativePath

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)    :: self

character(fnlen),INTENT(IN)           :: inpath
character(fnlen)                      :: outpath

integer(kind=irg)                     :: i, slen
character(1)                          :: todelim, fromdelim, c

slen = len(inpath)
outpath = ''
todelim = EMsoft_getEMsoftnativedelimiter(self)

if (todelim.eq.'\') then    ! ' 
  fromdelim = '/'
else
  fromdelim = '\'           ! '
end if

do i=1,slen
  c = inpath(i:i)
  if (c.eq.fromdelim) c = todelim
  outpath(i:i) = c
end do

end function EMsoft_toNativePath


!--------------------------------------------------------------------------
! EMsoft:local:EMsoft_fromNativePath.f90
!--------------------------------------------------------------------------
!
! FUNCTION: EMsoft_fromNativePath
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief convert a path string to the native format on this platform
!
!> @param inpath input path string
!
!> @date  02/16/16 MDG 1.0 original version
!--------------------------------------------------------------------------
function EMsoft_fromNativePath(self, inpath) result(outpath)
!DEC$ ATTRIBUTES DLLEXPORT :: EMsoft_fromNativePath

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)   :: self

character(fnlen),INTENT(IN)          :: inpath
character(fnlen)                     :: outpath

integer(kind=irg)                    :: i, slen
character(1)                         :: todelim, fromdelim, c

slen = len(inpath)
outpath = ''
fromdelim = EMsoft_getEMsoftnativedelimiter(self)

if (fromdelim.eq.'/') then
  todelim = '\'      ! ' 
else
  todelim = '/'
end if

do i=1,slen
  c = inpath(i:i)
  if (c.eq.fromdelim) c = todelim
  outpath(i:i) = c
end do

end function EMsoft_fromNativePath


!--------------------------------------------------------------------------
! EMsoft:local:EMsoft_path_init.f90
!--------------------------------------------------------------------------
!
! SUBROUTINE: EMsoft_path_init
!
!> @author Marc De Graef, Carnegie Mellon University
!
!> @brief Reads environment variables and sets appropriate path variables
!
!> @details This routine is called at the start of every EMsoft program; first,
!> we check to see whether or not the configuration file exists in .config/EMsoft.
!> If it exists, we simply parse the file and initialize various path parameters;
!> if it does not exist, then we create it with some default parameters and ask the
!> user for some others.
!
!> @param config optional
!
!> @date  05/05/15 MDG 1.0 new routine
!> @date  09/26/15 MDG 1.1 added .json config support
!> @date  11/20/15 MDG 1.2 added UserXXX variable support to json file
!> @date  02/25/16 MDG 1.3 removed support for environment variables; added creation of EMsoftConfig.json file
!> @date  02/25/16 MDG 1.4 added optional config parameter
!> @date  10/07/16 MDG 1.5 modified creation of configuration file for new split Public/Private develop setup
!> @date  08/31/18 MDG 1.6 replaced 'mv' command by 'ren' for Windows platform
!--------------------------------------------------------------------------

subroutine EMsoft_path_init(self, config)

use mod_io

IMPLICIT NONE

class(T_EMsoftClass),intent(inout)      :: self

logical,INTENT(IN),OPTIONAL             :: config

type(T_IOClass)                         :: Message 
character(fnlen)                        :: pathstring, dirstring, ep, EMsoftpathname, EMdatapathname, &
                                           username, userlocn, useremail, m
integer(kind=irg)                       :: i, error_cnt
logical                                 :: found, fexists, jexists
character(fnlen)                        :: confname, emsoftname, jsonname, jsonfilename, fname, cwd, &
                                           dirname, library, dataname, xtalname
character(3)                            :: release, develop
character(len=1)                        :: edp, tab, yesno 

edp = '"'
tab = CHAR(9)
yesno = 'n'
EMsoftpathname = ''
EMdatapathname = ''

! default names for the json configuration file
confname = SC_config
emsoftname = SC_EMsoft
jsonfilename = SC_jsonfilename

! get the config file name
jsonname = self % generateFilePath('Configpath',jsonfilename)

! test whether or not this file actually exists
inquire(file=trim(jsonname),exist=jexists)

write (*,*) 'jsonfilename : ', trim(jsonfilename)
write (*,*) 'jsonname = ', trim(jsonname), '  ', jexists

! if this routine is called with config=.TRUE. parameter, then that means that we
! must create a new EMsoftConfig.json file if it doesn't already exist; we will inform
! the user if it does exist, and rename the existing file
Message = T_IOClass()

if (present(config)) then
  if (config.eqv..TRUE.) then

    if (jexists) then
      call Message % printMessage( (/ '-------                                                                         ', &
                                      'WARNING: An older configuration file already exists in the .config/EMsoft folder', &
                                      '         The existing file will be renamed and a new file created.              '/) )
      call Message % ReadValue('          Do you want to continue ? (y/n) ', yesno, frm="(A1)" )
      if (yesno.eq.'n') then
        stop 'program terminated'
      end if
      call Message % printMessage('         Renaming old file to '//trim(jsonname)//'.save')
      call Message % printMessage((/ '         Creating new configuration file', &
                                     '-------                                 '/) )
      if (trim(self % EMsoftplatform).ne.'Windows') then   ! use the UNIX rename command 'mv oldname newname'
        call system('mv '//trim(jsonname)//' '//trim(jsonname)//'.save')
      else   ! on Windows the file rename command is 'ren oldname newname'
        call system('ren '//trim(jsonname)//' '//trim(jsonname)//'.save')
      end if
    end if

! look for the .config/EMsoft/EMsoftConfig.json file one step at a time
    dirstring = self % Homepathname
    call chdir(trim(dirstring))

! check for the .config folder
    dirname = trim(dirstring)//self % EMsoftnativedelimiter//trim(confname)
    inquire(file=trim(dirname),exist=fexists)
    if (.not.(fexists)) then
      call system('mkdir '//trim(dirname))
      call Message % printMessage(trim(dirname)//' folder did not exist and has been created')
    end if
    call chdir(trim(dirname))

! check for the EMsoft folder
    dirname = trim(dirname)//self % EMsoftnativedelimiter//SC_EMsoft
    inquire(file=trim(dirname),exist=fexists)
    if (.not.(fexists)) then
      call system('mkdir '//trim(dirname))
      call Message % printMessage(trim(dirname)//' folder did not exist and has been created')
    end if
    call chdir(trim(dirname))

! check whether or not the tmp folder exists...
    fname = trim(dirname)//self % EMsoftnativedelimiter//SC_tmp
    inquire(file=trim(fname),exist=fexists)
    if (.not.(fexists)) then
      call system('mkdir '//trim(fname))
      call Message % printMessage(trim(fname)//' folder did not exist and has been created')
    end if

! ok, so we have created the correct folder structure; now we need to generate the
! skeleton EMsoftConfig.json file, which then needs to be edited by the user

    release = 'No'
    develop = 'No'

! generate the json file; in principle we can replace all of these with Message class statements,
! but there's really no reason to do so ... 
    open(unit=dataunit,file=trim(jsonname),status='new',form='formatted')
    call Message % printMessage('{',redirect=dataunit)
    write (dataunit,"(A,A,'EMsoftpathname',A,': ',A,A,A,',')") tab, edp, edp, edp, &
       trim(EMsoft_getUserHomePath(self))//self % EMsoftnativedelimiter,edp
    write (dataunit,"(A,A,'EMXtalFolderpathname',A,': ',A,A,A,',')") tab, edp, edp, edp, &
       trim(EMsoft_getUserHomePath(self))//self % EMsoftnativedelimiter,edp
    write (dataunit,"(A,A,'EMdatapathname',A,': ',A,A,A,',')") tab, edp, edp, edp, &
       trim(EMsoft_getUserHomePath(self))//self % EMsoftnativedelimiter,edp
    write (dataunit,"(A,A,'EMtmppathname',A,': ',A,A,A,',')") tab, edp, edp, edp, &
       trim(fname)//self % EMsoftnativedelimiter,edp
    write (dataunit,"(A,A,'EMsoftLibraryLocation',A,': ',A,A,A,',')") tab, edp, edp, edp, &
       trim(EMsoft_getUserHomePath(self))//self % EMsoftnativedelimiter,edp
    write (dataunit,"(A,A,'Release',A,': ',A,A,A,',')") tab, edp, edp, edp, trim(release), edp
    write (dataunit,"(A,A,'Develop',A,': ',A,A,A,',')") tab, edp, edp, edp, trim(develop), edp

    call Message % printMessage((/ '-------                                                                ', &
                                   'Please respond to the following questions (each entry < 132 characters)'/) )
    call Message % ReadValue('  Enter your user name : ', username, frm="(A)" )
    write (dataunit,"(A,A,'UserName',A,': ',A,A,A,',')") tab, edp, edp, edp, trim(username), edp

    call Message % ReadValue('  Enter your email address : ', useremail, frm="(A)" )
    write (dataunit,"(A,A,'UserEmail',A,': ',A,A,A,',')") tab, edp, edp, edp, trim(useremail), edp

    call Message % ReadValue('  Enter your affiliation : ', userlocn, frm="(A)" )
    write (dataunit,"(A,A,'UserLocation',A,': ',A,A,A)") tab, edp, edp, edp, trim(userlocn), edp

    call Message % printMessage('}',redirect=dataunit)
    close(unit=dataunit,status='keep')

    call Message % printMessage( &
      (/' A skeleton EMsoftConfig.json file has been created in your .config/EMsoft folder.                  ', &
        ' You will need to edit this file to change the parameters from their default values.                ', &
        '                                                                                                    ', &
        ' - EMsoftpathname should point to the top folder of your EMsoft installation.                       ', &
        ' - EMdatapathname should point to where you want to keep all EMsoft output files.                   ', &
        '   Note that this folder should NOT be inside the EMsoftpathname folder!                            ', &
        '   You may leave this variable undefined (empty string) to force programs to                        ', &
        '   generate all files in the current working folder or to use full path file names.                 ', &
        ' - EMXtalFolderpathname should point to the folder that will contain the *.xtal files.              ', &
        ' - EMtmppathname should point to the tmp folder where the EMsoftConfig.json file is located.        ', &
        ' - EMsoftLibraryLocation is needed only if you have a fully functional IDL installation; this       ', &
        '   variable should then point to the location of the EMsoftLib.dylib or EMsoftLib.dll file.         ', &
        ' - One of the variables Develop and Release should be set to Yes, the other to No; if you are       ', &
        '   developing new EMsoft code using the EMsoftPrivate folder, then set Develop to Yes               ', &
        '   and Release to No to indicate Debug mode. for regular users: Develop=No, Release=Yes             ', &
        ' - EMNotify can be set to Slack or Email to send program completion messages to the user            ', &
        ' - EMSlackWebHookURL and EMSlackChannel are used for Slack messages (see Package Configuration wiki)', &
        '                                                                                                    ', &
        ' Make sure that each non-empty pathname ends with /, even on Windows platforms !                    ', &
        '                                                                                                    ', &
        ' Every EMsoft program will read this configuration file to figure out where things are located.     '/) )

  end if
end if

end subroutine EMsoft_path_init


end module mod_EMsoft
